// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

plugins.withId("io.github.gradle-nexus.publish-plugin") {
    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
                snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
                def sonatypeUser = findProperty("sonatypeUsername")?.toString() ?: System.getenv("SONATYPE_USERNAME")
                def sonatypePass = findProperty("sonatypePassword")?.toString() ?: System.getenv("SONATYPE_PASSWORD")
                if (sonatypeUser != null) {
                    username.set(sonatypeUser)
                }
                if (sonatypePass != null) {
                    password.set(sonatypePass)
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
